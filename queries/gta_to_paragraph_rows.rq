

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX coy: <https://schema.coypu.org/global#>
PREFIX gta: <https://schema.coypu.org/gta#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT
(?i AS ?entity)
(SAMPLE(?state_act_comment) AS ?state_act_label)
(SAMPLE(?it_label) AS ?intervention_type)
(SAMPLE(?id) AS ?implementation_date)
("harmful" AS ?evaluation)
(SAMPLE(?ij_label) AS ?implementing_jurisdiction)
(GROUP_CONCAT(DISTINCT ?aj_label; separator="; ") AS ?affected_jurisdictions)
(GROUP_CONCAT(DISTINCT ?sector_label; separator="; ") AS ?affected_sectors)

{
   ?i  rdf:type gta:Intervention ;
        rdfs:comment ?state_act_comment ;
        gta:hasInterventionType ?it ;
        gta:hasImplementationDate ?id ;
        gta:hasImplementingJurisdiction ?ij ;
        gta:hasAffectedJurisdiction ?aj ;
        gta:hasAffectedSector ?sector .
    ?ij rdfs:label ?ij_label .
    ?aj rdfs:label ?aj_label .
    ?it rdfs:label ?it_label .
    ?sector rdfs:label ?sector_label .
}
GROUP BY ?i
ORDER BY ?i
